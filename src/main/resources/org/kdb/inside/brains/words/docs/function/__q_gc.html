<pre><code class="language-txt">.Q.gc[]
</code></pre>
<p>Run garbage-collection and returns the amount of memory that was returned to the OS.</p>
<p>It attempts to coalesce pieces of the heap into their original allocation units and returns any units ≥64MB to the
    OS. Refer to \g (garbage collection mode) for details on how memory is created on the heap.</p>
<p>When secondary threads are configured and .Q.gc[] is invoked in the main thread, .Q.gc[] is automatically invoked in
    each secondary thread. If the call is instigated in a secondary thread, it affects that thread’s local heap
    only.</p>
<p>Example of garbage collection in the default deferred mode, using .Q.w[] to view memory stats:</p>

<pre><code class="language-q">q)a:til 10000000
q).Q.w[]
used| 67233056
heap| 134217728
peak| 134217728
wmax| 0
mmap| 0
syms| 534
symw| 23926
q).Q.gc[]
0
q)delete a from `.
`.
q).Q.gc[]
67108864
q).Q.w[]
used| 128768
heap| 67108864
peak| 134217728
wmax| 0
mmap| 0
syms| 535
symw| 23956
</code></pre>

<p>Depending on your data, memory can become fragmented and therefore difficult to release back to the OS. The following
    demonstrates an example:</p>
<pre><code class="language-q">q)v:{(10#&quot;a&quot;;10000#&quot;b&quot;)}each til 10000000;
q).Q.w[]
used| 164614358256
heap| 164752261120
peak| 164752261120
wmax| 0
mmap| 0
mphy| 270538350592
syms| 569
symw| 24934
q).Q.gc[]
134217728
q).Q.w[]
used| 164614358256
heap| 164618043392
peak| 164752261120
wmax| 0
mmap| 0
mphy| 270538350592
syms| 570
symw| 24964
q)v:v[;0] / just retain refs to the small char vectors of &quot;aaaaaaaa&quot;
q)/the vectors of &quot;bbb..&quot;s will come from the same memory chunks
q)/so can't be freed
q).Q.gc[]
134217728
q).Q.w[]
used| 454358256
heap| 164618043392
peak| 164752261120
wmax| 0
mmap| 0
mphy| 270538350592
syms| 570
symw| 24964
q)v:-8!v;0N!.Q.gc[];v:-9!v;.Q.w[] / serialize, release, deserialize
164483825664 / amount freed by gc
used| 454358848
heap| 738197504
peak| 164886478848
wmax| 0
mmap| 0
mphy| 270538350592
syms| 570
symw| 24964
</code></pre>

<p>If you have nested data, e.g. columns of char vectors, or much grouping, you may be fragmenting memory.</p>

<p><b>Since V3.3 2015.08.23</b> (Linux only) unused pages in the heap are dropped from RSS during .Q.gc[].</p>

<p><b>Since 4.1t 2022.07.01</b>, .Q.gc[0] can be used to perform a subset of operations performed by .Q.gc[] (i.e. only
    return unused blocks >= 64MB to os). This has the advantage of running return faster than .Q.gc[], but with the
    disadvantage of not defragmenting unused memory blocks of a smaller size (therefore may not free as much unused
    memory).</p>